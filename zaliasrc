# ~/.zaliasrc
# zsh aliases definitions, sourced from ~/.zshrc
# (interactive shells only)

# ls
alias ll='ls -lh'
alias la='ls -A'
alias l='ls -lAh'

# navigation
alias cl="cd; clear"
alias -g ...='../..'
alias -g ....='../../..'
alias -g .....='../../../..'
alias d='dirs -v'

# misc (from zsh-lovers)
alias j='jobs'
alias -g C='| wc -l'
alias -g G='| egrep'
alias -g L="| less"
alias -g LL='2>&1 | less'
alias -g TL='| tail -20'
alias -g T='| tail'
alias -g H='| head'
alias -g HL='|& head -20'
alias -g S='| sort'
alias -g N='>/dev/null'
alias -g DN='/dev/null'
alias -g NE='2>/dev/null'
alias -g NUL='>/dev/null 2>&1'
alias -g X0='| xargs -0'
alias -g X='| xargs'

# zsh tunning
alias mkdir='nocorrect mkdir'
alias smv='noglob zmv -W'
alias find='noglob find'
which wget N && alias wget='noglob wget'
which locate N && alias locate='noglob locate'
which youtube-dl N && alias youtube-dl='noglob youtube-dl'

# old keyboard settings
# now using:
# - dconf-editor org.gnome.desktop.input-sources
#   - sources: [('xkb', 'us'), ('xkb', 'ara+digits'), ('xkb', 'fr')]
#   - xkb-options: ['caps:ctrl_modifier', 'compose:102']
# - plus super-space for switching in keyboard shortcuts
#alias azerty='setxkbmap $( cat ~/.Xkbmap )'
#alias qwerty='setxkbmap -layout fr -option'

# misc
which qmv N && alias qmv='qmv -f do'
alias du1="du -x -h --max-depth 1 . | grep -E '^([0-9]*M|[0-9,]*G)' "
which ionice N && alias vnice='ionice -c3 nice'
alias -g GBA='$(git branch -a | sed '\''s/[* ]*\([^ ]*\).*/\1/'\'')'
which mocp N && alias wakeup='echo "mocp -p" |'
which dd N && which dcfldd N && alias dd='dcfldd'

# cd ripping
if which cdparanoia N; then
    rip () {
        local ext=${1-ogg}

        cdparanoia -B || return

        for i in *.wav; do
            local o=${i#track}
            o=${o%.cdda.wav}.$ext
            case $ext in
                ogg)    oggenc -q5 -o $o $i;;
                flac)   flac --best -o $o $i;;
                mp3)    lame $i $o;;
                *)      false;;
            esac && rm $i
        done
    }
fi

# screencasts with recordmydesktop
if which recordmydesktop N; then
    alias recordmydesktop='recordmydesktop --workdir ~/tmp'
    alias screencast='echo "*** ctrl-alt-s pour finir ***"; recordmydesktop --delay 5 --channels 1 --s_quality 0 --v_quality 16'
fi

if which parec oggenc N; then
    record() {
        local out=$(date -Iseconds)
        parec -d alsa_output.pci-0000_00_1f.3.analog-stereo.monitor \
            | oggenc -q3 -o ${1-$out}.ogg --raw -
    }
fi

# generate random password - base64 encoding
pwgen64() {
    head -c ${1-7} /dev/urandom | base64 | tr '+/' ',.' | sed 's/=*$//'
}

# generate random password - hex qwerty-awerty encoding
pwgen16() {
    head -c ${1-7} /dev/urandom | xxd -p | tr '0123456789abcdef' 'sdfghjklertyuiop'
}

# quick RFC access
if [ -d ~/doc/rfcs ]; then
    rfc() {
        view ~/doc/rfcs/rfc$1.txt
    }
fi

# convert any sound to wav
if which mplayer N; then
    sound2wav() {
        for f in "$@"; do
            mplayer -quiet -vo null -vc null -ao pcm:file="${f%.*}".wav $f
        done
    }
fi

# convert to PDF 1.4 (some old printers don't support better)
if which gs N; then
    pdf14() {
        local out
        case $# in
            1)
                out=${1%.pdf}-pdf14.pdf
                ;;
            2)
                out=$2
                ;;
            *)
                echo 'Usage: pdf14 infile [outfile]' 1>&2
                return 1
        esac
        gs -o $out -sDEVICE=pdfwrite -dCompatibilityLevel=1.4 $1
    }
fi

# sshfs mounts
if which sshfs N && which fusermount N; then
    smount () {
        mkdir -p ~/$1
        sshfs -o ro $1: ~/$1
    }
    sumount () {
        fusermount -uz ~/$1
    }
fi

# managing permissions
nox() { find $* \! -type d \! -type l -exec chmod a-x {} + }
pub() { find $* \! -type l -exec chmod a+r {} + , -type d -exec chmod a+x {} + }

# debian stuff
if which apt >/dev/null; then
    alias acs='apt-cache search'
    alias acw='apt-cache show'
    alias acp='apt-cache policy'
    alias dqw='dpkg-query -W -f='\''${Package;-40}${Version}\n'\'' | egrep'
    alias up='sudo apt autoremove --purge && sudo apt update && sudo apt upgrade && sudo apt autoremove --purge && sudo apt autoclean'
fi

# text to pdf
if which paps N && which ps2pdf N; then
    t2p() {
        paps $1 | ps2pdf - ${1%.txt}.pdf
    }
fi

# count cycles with valgrind
if which valgrind N; then
    ccount() {
        ( valgrind --tool=callgrind \
            --dump-line=no --callgrind-out-file=/dev/null \
            --collect-atstart=no --toggle-collect=main \
            $1) 2>&1 | sed -n -e 's/.*refs: *\([0-9,]*\)/\1/p'
    }
fi

# sml/nj
if which sml N; then
    which rlwrap N && alias sml='rlwrap sml'
    smlrun() {
        echo 'use "'$1'";' | sml
    }
fi

# get total running time of a bunch of mp3 files
if which mpg123 N; then
    mp3total() {
        local t=0
        for i in "$@"; do
            mpg123 -t $i 2>&1 | sed -n 's/^\[\([0-9]*\):\([0-9]*\)].*/\1 \2/p'
        done | while read m s; do
            t=$(( t + 60 * m + s ))
        done
        echo "$(( t / 3600 )):$(( (t % 3600) / 60 )):$(( t % 60 ))"
    }
fi

# get total running time of a bunch of ogg files
if which ogginfo N; then
    oggtotal() {
        local t=0
        for i in "$@"; do
            ogginfo $i |  sed -n 's/Playback length: \([0-9]*\)m:\([0-9]*\)\..*/\1 \2/p'
        done | while read m s; do
            t=$(( t + 60 * m + s ))
        done
        echo "$(( t / 3600 )):$(( (t % 3600) / 60 )):$(( t % 60 ))"
    }
fi

if which latex N; then
    # cleaning up after (La)TeX
    tc() {
        setopt local_options nullglob
        exts='.aux|.log|.dvi|.ps|.bbl|.out|.snm|.nav|.toc|.vrb|.blg|.glo|.idx|.mtc'
        exts="$exts|.maf|.stc?|.mtc?|.ptc?|ilg|ind|-blx.bib|.svn|.vex|.exa|.out"
        exts="$exts|.lot|.lof|.idx|.fdb_latexmk|.nlo|.nls|.run.xml|.fls"
        stars='.'
        if [[ $1 = "-r" ]]; then
            stars='**'
            shift
        fi
        basedir='.'
        if [[ -n $1 ]]; then
            basedir="$1"
        fi
        target=( $basedir/${~stars}/*(${~exts}) )
        if [[ -n $target ]]; then
            rm -- $target || return $?
        fi
        target=( $basedir/${~stars}/texput.* )
        if [[ -n $target ]]; then
            rm $target
        fi
    }

    # texmfhome management
    rmtexmfhome() {
        local texmfhome=$(kpsewhich --var-value TEXMFHOME)
        for pack in $@; do
            find $texmfhome -type d -name $pack -exec rm -r {} +
        done
    }
    lstexmfhome() {
        for dir in ~/texmf/tex/*/*; do
            echo ${dir##*/}
        done
    }
fi

# unzip archives created on Windows with Arabic file names
which unzip N && alias waunzip='unzip -O cp720'

# audacity not working well with wayland yet
# https://github.com/audacity/audacity/issues/459
which audacicy N && alias audacity='GDK_BACKEND=x11 audacity'

# create new worktree to review a PR
git-wta () {
    local dir="../$1"; shift
    git worktree add $dir "$@" && cd $dir
}
review-pr () {
    git-wta $1 public/pr/$1
}

if which exercism N; then
    exercism() {
        if [ "$1" = submit ]; then
            if ../check.sh; then
                command exercism "$@"
            else
                echo "../check.sh failed"
            fi
        elif [ "$1" = download ]; then
            cd $(command exercism "$@" | tail -n1)
        else
            command exercism "$@"
        fi
    }
fi

dodo () {
    sleep ${1:-30}m
    systemctl suspend || (sleep 5m; systemctl suspend)
}

# mamba from miniforge3 in ~/usr/miniforge3, linked in ~/bin
if which mamba N; then
    alias mamba-hook='eval "$(mamba shell hook --shell zsh)"'
fi

# avoid annoying error code on startup when last test was negative
true

# vim: ft=zsh:
